
group = 'com.github.hmcts'

repositories {
    mavenCentral()
}

def springBootVersion = '2.7.18'

dependencies {
    // Use JUnit test framework for unit tests
    testImplementation 'junit:junit:4.13.2'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.42'
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.42'
    implementation "org.springframework.boot:spring-boot-autoconfigure:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    api(group: 'com.github.hmcts', name: 'core-case-data-store-client', version: '5.2.0') {
        // The config generator needs only the types defined in the CCD client library,
        // not all the runtime spring dependencies used by the CCD client.
        transitive = false
    }

    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.42'
    testCompileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.42'
    testImplementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'
    testImplementation 'org.skyscreamer:jsonassert:1.5.3'
    testImplementation 'com.google.guava:guava:33.5.0-jre'
    testImplementation group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    testImplementation group: 'commons-io', name: 'commons-io', version: '2.20.0'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.27.6'
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: springBootVersion


    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.19.0'
    implementation group: 'org.objenesis', name: 'objenesis', version: '3.4'
    api 'com.google.guava:guava:33.5.0-jre'

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.20.0'
}

tasks.register('ccdConfigDiff', JavaExec) {
    group = 'verification'
    description = 'Compare CCD configuration directories using the Java diff comparator.'
    classpath = sourceSets.test.runtimeClasspath
    mainClass.set('uk.gov.hmcts.ccd.sdk.diff.CcdConfigDiffCli')

    doFirst {
        String expected = (project.findProperty('expectedDir') ?: project.findProperty('baseDir'))?.toString()
        String actual = (project.findProperty('actualDir') ?: project.findProperty('headDir'))?.toString()
        if (expected == null || actual == null) {
            throw new GradleException('Provide -PexpectedDir (or -PbaseDir) and -PactualDir (or -PheadDir) when running ccdConfigDiff.')
        }

        setArgs([expected, actual])
    }
}
