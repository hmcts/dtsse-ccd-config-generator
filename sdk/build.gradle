import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id("io.freefair.lombok") version "9.2.0" apply(false)
    id("org.springframework.boot") version "3.4.3" apply(false)
    id("io.spring.dependency-management") version "1.1.7" apply(false)
}

def sdkCheck = tasks.register('check');

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom(SpringBootPlugin.BOM_COORDINATES)
        }
    }

    if (project.name == 'decentralised-runtime') {
        apply plugin: 'org.springframework.boot'
    }

    version = System.getenv('RELEASE_VERSION')?.replace('refs/tags/', '') ?: 'DEV-SNAPSHOT'

    compileJava   {
        sourceCompatibility = 21
        targetCompatibility = 21
        options.compilerArgs += ["-Werror"]
    }

    checkstyle {
        maxWarnings = 0
        toolVersion = '13.0.0'
        sourceSets = [project.sourceSets.main]
    }

    group = 'com.github.hmcts'
    repositories {
        mavenCentral()
        maven {
            url = 'https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1'
        }
    }

    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    dependencies {
        // Use api to expose the BOM to consumers, and platform to pin consistent Spring versions.
        // Effect: downstream projects inherit the same dependency constraints, avoiding version drift.
        api platform(SpringBootPlugin.BOM_COORDINATES)
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    publishing {
        publications {
            Main(MavenPublication) {
                from components.java
                groupId = 'com.github.hmcts'
                artifactId = project.name
                version = project.version
            }
        }
        repositories {
            mavenLocal()
            maven {
                name = 'AzureArtifacts'
                url = uri('https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1')
                credentials {
                    username = System.getenv('AZURE_DEVOPS_ARTIFACT_USERNAME')
                    password = System.getenv('AZURE_DEVOPS_ARTIFACT_TOKEN')
                }
            }
        }
    }
    sdkCheck.get().dependsOn(tasks.check)
}

tasks.register('publishToMavenLocal') { task ->
    subprojects.each { subproject ->
        task.dependsOn(subproject.tasks.matching { it.name == 'publishToMavenLocal' })
    }
}

tasks.register('publishToAzureArtifacts') { task ->
    subprojects.each { subproject ->
        task.dependsOn(
            subproject.tasks.matching { it.name.endsWith('ToAzureArtifactsRepository') }
        )
    }
}
