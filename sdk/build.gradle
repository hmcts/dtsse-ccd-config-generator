import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id("io.freefair.lombok") version "9.2.0" apply(false)
    id("org.springframework.boot") version "3.4.3" apply(false)
    id("io.spring.dependency-management") version "1.1.7" apply(false)
    id 'jacoco'
}

repositories {
    mavenCentral()
}

def sdkCheck = tasks.register('check');

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'jacoco'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom(SpringBootPlugin.BOM_COORDINATES)
            mavenBom('org.testcontainers:testcontainers-bom:2.0.3')
        }
    }

    version = System.getenv('RELEASE_VERSION')?.replace('refs/tags/', '') ?: 'DEV-SNAPSHOT'

    compileJava   {
        sourceCompatibility = 21
        targetCompatibility = 21
        options.compilerArgs += ["-Werror"]
    }

    checkstyle {
        maxWarnings = 0
        toolVersion = '13.2.0'
        sourceSets = [project.sourceSets.main]
    }

    group = 'com.github.hmcts'
    repositories {
        mavenCentral()
        maven {
            url = 'https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1'
        }
    }

    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    java {
        withJavadocJar()
        withSourcesJar()
    }

    publishing {
        publications {
            Main(MavenPublication) {
                from components.java
                groupId = 'com.github.hmcts'
                artifactId = project.name
                version = project.version
            }
        }
        repositories {
            mavenLocal()
            maven {
                name = 'AzureArtifacts'
                url = uri('https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1')
                credentials {
                    username = System.getenv('AZURE_DEVOPS_ARTIFACT_USERNAME')
                    password = System.getenv('AZURE_DEVOPS_ARTIFACT_TOKEN')
                }
            }
        }
    }
    sdkCheck.get().dependsOn(tasks.check)

    jacocoTestReport {
        // Include all Test task exec data (test, functionalTest, etc.)
        executionData.from = fileTree(dir: layout.buildDirectory.dir('jacoco'), includes: ['*.exec'])
        reports {
            xml.required = true
            html.required = true
        }
    }
}

tasks.register('publishToMavenLocal') { task ->
    subprojects.each { subproject ->
        task.dependsOn(subproject.tasks.matching { it.name == 'publishToMavenLocal' })
    }
}

tasks.register('publishToAzureArtifacts') { task ->
    subprojects.each { subproject ->
        task.dependsOn(
            subproject.tasks.matching { it.name.endsWith('ToAzureArtifactsRepository') }
        )
    }
}

tasks.register('jacocoAggregateReport', JacocoReport) {
    description = 'Generates an aggregate JaCoCo coverage report for all SDK subprojects.'
    group = 'verification'

    subprojects.each { subproject ->
        dependsOn subproject.tasks.withType(Test)
    }

    def execFiles = subprojects.collect {
        fileTree(dir: it.layout.buildDirectory.dir('jacoco'), includes: ['*.exec'])
    }
    // Include e2e cftlibTest exec data if available.
    execFiles += fileTree(dir: "${rootProject.projectDir}/../test-projects/e2e/build/jacoco",
                          includes: ['*.exec'])

    executionData.from = files(execFiles)

    def srcDirs = subprojects.collect { it.sourceSets.main.allSource.srcDirs }.flatten()
    def classDirs = subprojects.collect { it.sourceSets.main.output }.flatten()

    sourceDirectories.from = files(srcDirs)
    classDirectories.from = files(classDirs)

    reports {
        xml.required = true
        html.required = true
        xml.outputLocation = layout.buildDirectory.file('reports/jacoco/aggregate/jacocoAggregateReport.xml')
        html.outputLocation = layout.buildDirectory.dir('reports/jacoco/aggregate/html')
    }
}
