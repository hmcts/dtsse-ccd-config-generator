plugins {
    id("io.freefair.lombok") version "9.0.0" apply(false)
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'io.freefair.lombok'

    version System.getenv('RELEASE_VERSION')?.replace('refs/tags/', '') ?: 'DEV-SNAPSHOT'

    compileJava   {
        sourceCompatibility = 17
        targetCompatibility = 17
        options.compilerArgs += ["-Werror"]
    }

    checkstyle {
        maxWarnings = 0
        toolVersion '12.1.0'
        sourceSets = [project.sourceSets.main]
    }

    group = 'com.github.hmcts'
    repositories {
        mavenCentral()
        maven {
            url 'https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1'
        }
    }

    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    java {
        withJavadocJar()
        withSourcesJar()
    }

    publishing {
        publications {
            Main(MavenPublication) {
                from components.java
                groupId 'com.github.hmcts'
                artifactId project.name
                version version
            }
        }
        repositories {
            mavenLocal()
            maven {
                name = 'AzureArtifacts'
                url = uri('https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1')
                credentials {
                    username = System.getenv('AZURE_DEVOPS_ARTIFACT_USERNAME')
                    password = System.getenv('AZURE_DEVOPS_ARTIFACT_TOKEN')
                }
            }
        }
    }
}

tasks.register('check') {
    dependsOn(':ccd-config-generator:check')
    dependsOn(':ccd-gradle-plugin:check')
}

tasks.register('publishToMavenLocal') { task ->
    subprojects.each { subproject ->
        task.dependsOn(subproject.tasks.matching { it.name == 'publishToMavenLocal' })
    }
}

tasks.register('publishToAzureArtifacts') { task ->
    subprojects.each { subproject ->
        task.dependsOn(
            subproject.tasks.matching { it.name.endsWith('ToAzureArtifactsRepository') }
        )
    }
}
